 Fix←{name←⍺ ⋄ src←⍵
   VS∆PS←⊂'\Program Files (x86)\Microsoft Visual Studio\'
   VS∆PS,¨←,'2019\' '2017\'∘.,'Enterprise' 'Professional' 'Community'
   VS∆PS,¨←⊂'\VC\Auxiliary\Build\vcvarsall.bat'
   VS∆PS,←⊂'\Program Files (x86)\Microsoft Visual Studio 14.0\VC\vcvarsall.bat'
   Cmp←{_←1 ⎕NDELETE f←⍺,soext⍬
    out←GC{⍵⊣⍞←'G'}ir←TT{⍵⊣⍞←'C'}a n s src←PS ∊⍵,¨⎕UCS 10⊣⍞←'P'
    _←out put ⍺,'.cpp'
    _←(⍎opsys'vsc' 'gcc' 'clang')⍺ ⋄ ⎕←⍪⊃⎕NGET(⍺,'.log')1 ⋄ ⎕NEXISTS f:n
    'COMPILE ERROR' ⎕SIGNAL 22}
   MKA←{mka⊂⍵} ⋄ EXA←{exa ⍬ ⍵}
   Display←{⍺←'Co-dfns' ⋄ W←w_new⊂⍺ ⋄ 777::w_del W
    w_del W⊣W ⍺⍺{w_close ⍺:⍎'⎕SIGNAL 777' ⋄ ⍺ ⍺⍺ ⍵}⍣⍵⍵⊢⍵}
   LoadImage←{⍺←1 ⋄ ~⎕NEXISTS ⍵:⎕SIGNAL 22 ⋄ loadimg ⍬ ⍵ ⍺}
   SaveImage←{⍺←'image.png' ⋄ saveimg ⍵ ⍺}
   Image←{~2 3∨.=≢⍴⍵:⎕SIGNAL 4 ⋄ (3≠⊃⍴⍵)∧3=≢⍴⍵:⎕SIGNAL 5 ⋄ ⍵⊣w_img ⍵ ⍺}
   Plot←{2≠≢⍴⍵:⎕SIGNAL 4 ⋄ ~2 3∨.=1⊃⍴⍵:⎕SIGNAL 5 ⋄ ⍵⊣w_plot (⍉⍵) ⍺}
   Histogram←{⍵⊣w_hist ⍵,⍺}
   opsys←{⍵⊃⍨'Win' 'Lin' 'Mac'⍳⊂3↑⊃'.'⎕WG'APLVersion'}
   soext←{opsys'.dll' '.so' '.dylib'}
   mkna←{(⍺,soext⍬),'|',('∆'⎕R'__'⊢⍵),'_cdf P P P'}
   Rtm∆Init←{
    _←'w_new'⎕NA'P ',(⍵,soext⍬),'|w_new <C[]'
    _←'w_close'⎕NA'I ',(⍵,soext ⍬),'|w_close P'
    _←'w_del'⎕NA(⍵,soext⍬),'|w_del P'
    _←'w_img'⎕NA(⍵,soext⍬),'|w_img <PP P'
    _←'w_plot'⎕NA(⍵,soext⍬),'|w_plot <PP P'
    _←'w_hist'⎕NA(⍵,soext⍬),'|w_hist <PP F8 F8 P'
    _←'loadimg'⎕NA(⍵,soext⍬),'|loadimg >PP <C[] I'
    _←'saveimg'⎕NA(⍵,soext⍬),'|saveimg <PP <C[]'
    _←'exa'⎕NA(⍵,soext⍬),'|exarray >PP P'
    _←'mka'⎕NA'P ',(⍵,soext⍬),'|mkarray <PP'
    _←'FREA'⎕NA(⍵,soext⍬),'|frea P'
    _←'Sync'⎕NA(⍵,soext⍬),'|cd_sync'
    0 0 ⍴ ⍬}
   MkNS←{ns←#.⎕NS⍬ ⋄ _←'∆⍙'ns.⎕NS¨⊂⍬ ⋄ ∆ ⍙←ns.(∆ ⍙) ⋄ ∆.names←(0⍴⊂''),(1=1⊃⍵)⌿0⊃⍵
    fns←'Rtm∆Init' 'MKA' 'EXA' 'Display' 'LoadImage' 'SaveImage' 'Image' 'Plot'
    fns,←'Histogram' 'soext' 'opsys' 'mkna'
    _←∆.⎕FX∘⎕CR¨fns ⋄ ∆.(decls←⍺∘mkna¨names) ⋄ _←ns.⎕FX¨(⊂''),⍺∘mkf¨∆.names
    _←∆.⎕FX'Z←Init'('Z←Rtm∆Init ''',⍺,'''')'→0⌿⍨0=≢names' 'names ##.⍙.⎕NA¨decls'
    ns}
   mkf←{fn←(⍺,soext⍬),'|',('∆'⎕R'__'⊢⍵),'_dwa ' ⋄ mon dya←⍵∘,¨'_mon' '_dya'
    z←('Z←{A}',⍵,' W')(':If 0=⎕NC''⍙.',mon,'''')
    z,←(mon dya{'''',⍺,'''⍙.⎕NA''',fn,⍵,' <PP'''}¨'>PP P' '>PP <PP'),⊂':EndIf'
    z,':If 0=⎕NC''A'''('Z←⍙.',mon,' 0 0 W')':Else'('Z←⍙.',dya,' 0 A W')':EndIf'}
   tie←{0::⎕SIGNAL ⎕EN ⋄ 22::⍵ ⎕NCREATE 0 ⋄ 0 ⎕NRESIZE ⍵ ⎕NTIE 0}
   put←{s←(¯128+256|128+'UTF-8'⎕UCS ⍺)⎕NAPPEND(t←tie ⍵)83 ⋄ 1:r←s⊣⎕NUNTIE t}
   ccf←{' -o ''',⍵,'.',⍺,''' ''',⍵,'.cpp'' -laf',AF∆LIB,' > ',⍵,'.log 2>&1'}
   cci←{'-I''',AF∆PREFIX,'/include'' -L''',AF∆PREFIX,'/lib64'' '}
   cco←'-std=c++17 -Ofast -g -Wall -fPIC -shared -Wno-parentheses '
   cco,←'-Wno-misleading-indentation '
   ucc←{⍵⍵(⎕SH ⍺⍺,' ',cco,cci,ccf)⍵}
   gcc←'g++'ucc'so'
   clang←'clang++'ucc'dylib'
   vsco←{z←'/W3 /wd4102 /wd4275 /Od /Zc:inline /Zi /FS /Fd"',⍵,'.pdb" '
    z,←'/WX /MD /EHsc /nologo /std:c++latest '
    z,'/I"%AF_PATH%\include" /D "NOMINMAX" /D "AF_DEBUG" '}
   vslo←{z←'/link /DLL /OPT:REF /INCREMENTAL:NO /SUBSYSTEM:WINDOWS '
    z,←'/LIBPATH:"%AF_PATH%\lib" /DYNAMICBASE "af', AF∆LIB, '.lib" '
    z,'/OPT:ICF /ERRORREPORT:PROMPT /TLBID:1 '}
   vsc0←{~∨⌿b←⎕NEXISTS¨VS∆PS:'VISUAL C++?'⎕SIGNAL 99 ⋄ '""','" amd64',⍨⊃b⌿VS∆PS}
   vsc1←{' && cd "',(⊃⎕CMD'echo %CD%'),'" && cl ',(vsco ⍵),'/fast "',⍵,'.cpp" '}
   vsc2←{(vslo ⍵),'/OUT:"',⍵,'.dll" > "',⍵,'.log""'}
   vsc←{⎕CMD('%comspec% /C ',vsc0,vsc1,vsc2)⍵}
   name MkNS name Cmp src}
