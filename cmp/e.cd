⍝ A  B  E  F  G  L  M  N  O  P  V  Z
⍝ 0  1  2  3  4  5  6  7  8  9 10 11
tt←{⍞←'C' ⋄ ((d t k n)exp sym)←⍵ ⋄ I←{(⊂⍵)⌷⍺}

 ⍝ Convert to Parent Vector 
 _←2{l[⍵[i]]←⍵[¯1+i←⍸0,2=⌿i]⊣p[⍵]←⍺[i←⍺⍸⍵]}⌿⊢∘⊂⌸d⊣p←l←⍳≢d

 ⍝ Binding Table
 bv←I@{1=t[⍵]}⍣≡⍨i@(p[i←⍸1=t[p]])⍳≢p

 ⍝ Mark and record top-level bindings
 tlb←⍸(t=1)∧{⍵=p[⍵]}p I@{3≠t[⍵]}⍣≡⍳≢p ⋄ rn←⍸(t=3)∧p=⍳≢p
 tlx←k[tlb],n[tlb],⍪p I⍣≡tlb ⋄ k[tlb]×←2

 ⍝ Lift Functions
 i←⍸(t=3)∧p≠⍳s←≢p ⋄ l←i(s+⍳)@{⍵∊i}l ⋄ p l(⊣,I)←⊂i ⋄ t k,←10 1⍴⍨¨≢i 
 n,←i ⋄ p[i]←i ⋄ l[j]←⊃(⌽i),j←⍸(p=⍳≢p)∧l=⍳≢l ⋄ l[i]←(≢i)↑(⊃i),i

 ⍝ Wrap Return Expressions
 i←⍸((t∊0 2)∧t[p]=3)∨(t[p]∊3 4)∧(~(⍳≢l)∊¯1@{⍵=⍳≢⍵}l)∧(t∊0 2)∨(t=1)∧k=0
 p,←p[i] ⋄ p[i]←(≢l)+⍳≢i ⋄ l←i((≢l)+⍳)@{⍵∊i}l ⋄ l,←l[i] ⋄ l[i]←i
 t k n,←2 0 0⍴⍨¨≢i

 ⍝ Lift Guard Tests
 fi←p[i←⍸(t∊⍳3)∧(l=⍳≢l)∧t[p]=4] ⋄ l←j@(j←⍸l∊i)⊢l 
 l[i]←fl[j]@(j←⍸fi≠fl←l[fi])⊢i ⋄ l[fi]←i ⋄ n[fi]←i ⋄ p[i]←p[fi]

 ⍝ Lift Expressions
 i←⍸(t∊8,⍳3)∧m←~t[p]∊3 4 ⋄ xw←x@(l[x])⊢x@(x←⍸m)⊢l ⋄ l←i((≢l)+⍳)@{⍵∊i}l
 p,←∆←p[i] ⋄ l,←l[i] ⋄ t,←10⍴⍨≢i ⋄ k,←(8∘=∨k[i]∧1∘=)t[i] ⋄ n,←i
 l[∪∆]←∆⊢∘⊃⌸i ⋄ net←{~t[⍵]∊8,⍳5} ⋄ wk←xw I p∘I@(xw∘I=⊢)⍣≡
 l[j]←((j×⊢)+×∘~)∘net⍨wk@net⍣≡wk⊢j←i~∆ ⋄ p[i]←p I@(~3 4∊⍨t∘I)⍣≡∆

 ⍝ Resolve Local Names
 vl←p I@{(l=⍳≢l)∧t[p]=4}l
 loc←{m←⍺{(t[⍵]=1)∧n[⍵]=⍺⍺} ⋄ b+⍺×0=b←bv[×∘m⍨vl I@(~m)⍣≡l[⍵]]}
 n[i]←n[i]loc p[i←⍸(t=10)∧n<¯4]

 ⍝ Collapse Variable and Binding Reference Chains
 n←bv I@{t[0⌈⍵]=1}I@{t[0⌈⍵]=10}⍣≡⍨n

 ⍝ Inline Functions

 ⍝ Build call sites, operator reference, closure, and free variable tables
 t3←t[0⌈n]=3 ⋄ ov←(8=tp←t[p])∧lv←(t=10)∧n≥0 ⋄ ev←lv∧(tp=2)∨(tp=1)∧k[p]=2
 xc←((m[l]∧(~m)∧2=⊢)∨m∧1=⊢)k[p]⊣m←l=⍳≢l ⋄ i←⍸ov∧t3∧xc ⋄ of←n[i] ⋄ os←p[i]
 i←⍸ov∧(nos←n∊os)∨t3∧~xc ⋄ on←n[i] ⋄ oi←p[i] ⋄ i←⍸ov ⋄ pom←p[i] ⋄ nom←n[i]
 i←⍸ev∧t3∨nos ⋄ cf←n[i] ⋄ cs←p[i] ⋄ x←⍸ev
 _←{(oi,←(≢¨g)[i]⌿pom)(on,←∊(g←(⊃⊢∘⊂⌸⌿⍵),⊂⍬)[i←(∪⊃⍵)⍳nom])}⍣≡oi on
 i←(∪oi)⍳n[x] ⋄ g←(oi⊢∘⊂⌸on),⊂⍬ ⋄ on←∊g[i] ⋄ oi←(≢¨g)[i]⌿p[x]
 fi←⍬ ⋄ ftn←0 2⍴⍬ ⋄ ci←⍬ ⋄ ctvr←0 3⍴⍬

 ⍝ Propagate and ground free references up the lexical stack
 _←{g←(⊃⊢∘⊂⌸⌿⍵),⊂2/⍪⍬ ⋄ x←(∪⊃⍵)∘⍳ ⋄ fi ftn⍪←⍵⍪¨((≢¨g)[i]⌿os)(⊃⍪⌿g[i←x of])
  s←⍬ ⋄ tv←2/⍪⍬ ⋄ r←⍬
  _←{ci,←s,←ns←(≢¨g)[i←x⊃⍵]⌿1⊃⍵
   ctvr⍪←ntv,r,←nr←n I@{t[0⌈⍵]=10}(⊢/tv⍪←ntv←⊃⍪/g[i])loc ns
   (nr[i])(ns[i←⍸(nr≥0)∧1=⊣/ntv])}⍣{(0=≢⊃⍺)∨⍺≡⍵}(cf,on,⊢/ctvr)(cs,oi,ci)
  (p[s[i]])(tv[i←⍸r<0;])
 }⍣{(0=≢⊃⍺)∨⍺≡⍵}(p I@{t[⍵]≠3}⍣≡i)(k[i],⍪n[i←⍸(t=10)∧n<¯4])

 ⍝ Inline Primitive References
 i←⍸(t=10)∧(n≥0)∧t[0⌈n]=9 ⋄ t[i]←9 ⋄ k[i]←0 ⋄ n[i]←n[n[i]] 

 ⍝ Propagate constants
 ⍝ Fold constants
 ⍝ Dead, useless code elimination
 ⍝ Allocate frames

 ⍝ Extend top-level exports with their bindings
 tlx,←bv[tlb] ⋄ k[tlb](⊣+⊢×0=⊣)←3

 p l t k n exp sym tlx rn fi ftn ci ctvr oi on}
